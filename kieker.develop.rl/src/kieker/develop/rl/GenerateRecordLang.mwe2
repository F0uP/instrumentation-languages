/***************************************************************************
 * Copyright 2017 Kieker Project (http://kieker-monitoring.net)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ***************************************************************************/
module kieker.develop.rl.GenerateRecordLang

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.emf.mwe.utils.*

var rootPath = ".."

Workflow {
	bean = StandaloneSetup {
		scanClassPath = true
		
		platformUri = "."

		uriMap = {
			from = "platform:/plugin/org.eclipse.emf.ecore/model/Ecore.genmodel"
			to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
		}

		uriMap = {
			from = "platform:/plugin/org.eclipse.emf.ecore/model/Ecore.ecore"
			to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.ecore"
		}

		uriMap = {
			from = "platform:/plugin/kieker.develop.semantics/model/SemanticAnnotation.ecore"
			to = "platform:/resource/kieker.develop.semantics/SemanticAnnotation.ecore"
		}

		uriMap = {
			from = "platform:/plugin/kieker.develop.semantics/model/SemanticAnnotation.genmodel"
			to = "platform:/resource/kieker.develop.semantics/SemanticAnnotation.genmodel"
		}

		registerGenModelFile = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
		registerEcoreFile = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.ecore"

		registerGenModelFile = "platform:/resource/kieker.develop.semantics/model/SemanticAnnotation.genmodel"
		registerEcoreFile = "platform:/resource/kieker.develop.semantics/model/SemanticAnnotation.ecore"
	}

	component = XtextGenerator {
		standaloneSetup = XtextGeneratorStandaloneSetup {
			scanClasspath = true
		}
	
		configuration = {
			project = StandardProjectConfig {
				mavenLayout = false
				createEclipseMetaData = true
				
				baseName = "kieker.develop.rl"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}

			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "kieker.develop.rl.RecordLang"
			fileExtensions = "irl"

			fragment = types.TypesGeneratorFragment2 auto-inject {}

			referencedResource = "platform:/resource/kieker.develop.semantics/model/SemanticAnnotation.genmodel"
			serializer = {
				generateStub = false
			}
			validator = {
			// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
		}
	}
}
