/***************************************************************************
 * Copyright 2017 Kieker Project (http://kieker-monitoring.net)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ***************************************************************************/
grammar kieker.develop.al.AspectLang with org.eclipse.xtext.common.Terminals

generate aspectLang "http://www.cau.de/cs/se/instrumentation/al/AspectLang"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.cau.de/cs/se/instrumentation/rl/RecordLang" as irl
// import "platform:/resource/kieker.develop.rl/model/generated/RecordLang.ecore" as irl
import "http://mappingmodel.tools.kieker/ial/mapping" as mapping
import "http://www.kieker-monitoring.net/kieker/develop/semantic-annotations" as semantics

AspectModel:
	'package' name = QualifiedName
	(imports += Import)*
	(sources += ApplicationModelHandle)*
	(advices += Advice | pointcuts+=Pointcut | aspects += Aspect)*
;

Import:
	'import' importedNamespace = QualifiedNameWithWildcard
;

ApplicationModelHandle:
	'map' handler=ID 'model' name=ID model=STRING ('trace' traceModel=STRING)?
;

Aspect:
	'aspect' pointcut=[Pointcut|QualifiedName] ':' advices+=[Advice|QualifiedName] (',' advices+=[Advice|QualifiedName])* 
;

/* ------------------------------- */
/* advice                          */

Advice:
	'advice' name=ID '{'
		collectors+=Collector*
	'}'
;

Collector:
	insertionPoint=InsertionPoint events+=Event+
;

Event:
	type=[irl::EventType|QualifiedName] ('(' properties+=Property (',' properties+=Property)* ')')?
;

Property:
	Literal | annotation=[semantics::Annotation|ID]
;

enum InsertionPoint: 
	BEFORE = 'before' |
	AFTER = 'after' |
	AFTER_EXCEPTION = 'exception'
;

/* ------------------------------- */
/* pointcut                        */

Pointcut:
	(annotation=Annotation)?
	'pointcut' name=ID
	'class' model=[ApplicationModelHandle|ID] '.' location=LocationQuery 
	('operation' operation=OperationQuery)?
;

Annotation:
	'@' name='technology' technologies+=[semantics::Technology|ID] (',' technologies+=[semantics::Technology|ID])?
;

Query:
	LocationQuery | CompositionQuery
;

LocationQuery:
	node=Node (('.' specialization=LocationQuery) | (composition=CompositionQuery))?
;

CompositionQuery: {CompositionQuery}
	'{' (subQueries += SubQuery)* '}'	
;

SubQuery:
	modifier=QueryModifier location=LocationQuery	
;

enum QueryModifier:
	INCLUDE = '+' |
	EXCLUDE = '-'
;

OperationQuery: {OperationQuery}
	modifier=[mapping::OperationModifier|ID]?
	returnType=TypeReference? 
	(
		(
			operationReference=[mapping::Operation|ID] 
			('(' parameterQueries+=ParameterQuery (',' parameterQueries+=ParameterQuery)* ')')? 
		) | 
		'*'
	)
;

ParameterQuery: {ParameterQuery}
	modifier=[mapping::ParameterModifier|ID]?
	(type=TypeReference | '*') 
	parameter=[mapping::Parameter|ID]?
;

Node:
	(SubPathNode | WildcardNode | ParentNode | ContainerNode) ('[' constraint=PropertyConstraint ']')?  
;

ContainerNode:
	container=[mapping::Feature|ID]
;

WildcardNode: {WildcardNode}
	'*'
;

SubPathNode: {SubPathNode}
	'**'
;

ParentNode: {ParentNode}
	'up'
;

PropertyConstraint: 
	PropertyConstraintCompare ({PropertyConstraint.left=current} logic=LogicOperator right=PropertyConstraintCompare)?
;

enum LogicOperator:
	AND = '&&' |
	OR = '||'
;

PropertyConstraintCompare:
	ConstraintElement ({PropertyConstraintCompare.left=current} compare=CompareOperator right=ConstraintElement)?
;

enum CompareOperator:
	EQ = '=' |
	LIKE = '~' |
	NE = '!=' |
	GR = '>' |
	LW = '<' |
	GE = '>=' |
	LE = '<='
;

ConstraintElement:
	Literal | LocalQuery
;

LocalQuery:
	(locationQuery=LocationQuery)? (property=ModelProperty | typeof=Typeof)
;

ModelProperty:
	 '#' reference=[mapping::Feature|ID]
;

Typeof:
	'typeof' '(' reference=TypeReference ')' 
;

/* ------------------------------- */
/* literals                        */

Literal:
	StringLiteral | IntLiteral | FloatLiteral
;

FloatLiteral:
	value=FLOAT
;

IntLiteral:
	value=INT
;

StringLiteral:
	value=STRING
;

TypeReference:
	context=[ApplicationModelHandle|ID] '.' reference=[mapping::NamedType|ID] (collection?='[]')?
;

/* ------------------------------- */
/* Terminals and literal rules     */

QualifiedName:
  ID (=>'.' ID)*;

QualifiedNameWithWildcard:
	QualifiedName ('.' '*')?
;

// Float Terminal    
// Make sure the Float rule does not shadow the INT rule
terminal FLOAT returns ecore::EFloatObject :
    NUMBER+ ('.' NUMBER*) (("e"|"E") ("+"|"-")? NUMBER+)? 'f'? |
    NUMBER+ 'f';
    
// Number Terminal
terminal fragment NUMBER :
    '0'..'9';


// Integer Terminal
// An INT is a list of numbers.   
@Override 
terminal INT returns ecore::EInt:
    NUMBER+;

